buildscript {
	ext {
		springBootVersion = '1.2.5.RELEASE'
	}

	repositories {
		jcenter()
		mavenCentral()
	}
	
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE"
	}
}

apply plugin: 'eclipse'

subprojects {
	apply plugin: 'maven'
	apply plugin: 'java'
	apply plugin: 'eclipse'
//	apply plugin: 'eclipse-wtp'
	apply plugin: 'spring-boot'
	apply plugin: 'io.spring.dependency-management'
	
	version = '0.0.1-SNAPSHOT'
	group = 'org.cinnamon'
	
	repositories {
		jcenter()
		mavenCentral()
	}
	
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	
	
	sourceSets {
		generated {
			java {
				srcDirs = ["src/main/generated"]
			}
		}
	}
	
	configurations {
		querydslapt
	}
	
	
	dependencies {
		testCompile "org.springframework.boot:spring-boot-starter-test"
		
		querydslapt 'com.mysema.querydsl:querydsl-apt:3.6.7'
	}
	
	bootRepackage  {
		enabled = false
	}
	
	
	//--queryDSL query객체 생성관련 태스크------
	task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
		source = sourceSets.main.java
		classpath = configurations.compile + configurations.querydslapt
		options.compilerArgs = [
			"-proc:only",
			"-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
		]
		destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
	}
	
	compileJava {
		dependsOn generateQueryDSL
		source generateQueryDSL.destinationDir
	}
	
	compileGeneratedJava {
		dependsOn generateQueryDSL
		options.warnings = false
		classpath += sourceSets.main.runtimeClasspath
	}
	
	clean {
		delete sourceSets.generated.java.srcDirs
	}
	//----------------------------------
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.6'
}