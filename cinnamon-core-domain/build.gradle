sourceSets {
	generated {
		java {
			srcDirs = ["src/main/generated"]
		}
	}
}

configurations {
	querydslapt
}

dependencies {
	compile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
	compile "com.mysema.querydsl:querydsl-jpa:3.6.6"
	compile 'com.fasterxml.jackson.core:jackson-annotations'
	
	querydslapt 'com.mysema.querydsl:querydsl-apt:3.6.6'
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
	source = sourceSets.main.java
	classpath = configurations.compile + configurations.querydslapt
	options.compilerArgs = [
		"-proc:only",
		"-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
	]
	destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
	dependsOn generateQueryDSL
	source generateQueryDSL.destinationDir
}

compileGeneratedJava {
	dependsOn generateQueryDSL
	options.warnings = false
	classpath += sourceSets.main.runtimeClasspath
}

clean {
	delete sourceSets.generated.java.srcDirs
}